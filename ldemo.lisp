;;;; ldemo.lisp

(in-package #:ldemo)

(defun expand-path-old (iterations initial-string &rest replacement-rules)
  "Given an initial string and replacement rules recursivly iterate
   <initial-string> with rules sequentially <iterations> times. A rule
   is a pair with term to be replaced and its replacement."
  (dotimes (i iterations)
    (dolist (rule replacement-rules)
      (setf initial-string (str:replace-all (car rule)
                                            (cdr rule)
                                            initial-string))))
  initial-string)


(defun expand-path (iterations initial-string &rest replacement-rules)
  "Given an initial string and replacement rules recursivly iterate
   <initial-string> with rules sequentially <iterations> times. A rule
   is a pair with term to be replaced and its replacement."
  (flet ((lookup (char)
           (or (cdr (assoc char replacement-rules))
               char)))
    (dotimes (i iterations)
      (setf initial-string
            (format nil "狺磲ъ轶＇祜镫躔轭轸獒飙篝蜷铉┅┅轭轸獒飙篝蜷铉ㄤ彐躅潋狩扉铄ㄣ犷鲠泔祜疱瞽鏖漪瑭磲脲扉铄汜铞狍啜博烘殪泔祜瑚殇翳疱瞽鏖漪瑭换羽邈獒麸翳轶溴盹ㄤ彐躅潋狩飙簌篝屙ㄡ铉戾篝屦趱螋飙篝蜷铉祜镳瑚轸篝徙啜趱螋瑚轸沲蝌孱舡趱螋ㄦ轵篝篝徙氅烘矧横泸矬飙篝蜷铉轰箦翩沲蝌孱舡趱螋ㄦ轵篝篝徙氅ㄣ狍ǎ塬祠沲蝌孱舡趱螋犷珈濠ǎ墉螋沲蝌孱舡趱螋犷珈濠ǎ芷ㄦ沲蝌孱舡趱螋篝屦┅ǎ苒瘐箬ㄣ祜铄趱螋戾沲蝌孱舡趱螋篝徙氅ǎ茌痫篝徙氅┅┅ㄤ彐躅磲轭ī鏖翳铒溏蹰ê糸綮⑻御篝屙燥箦舡珏镯弭蝙麒綦赴栋癌戾舄è筱磲脲轭篝犷沐筱蝻祆邃汜铞狍┅ㄣㄣ犷鲠筱┅ㄤ蜥鳝骖灬礅溽é蝈篝狒趄殁螬ㄡ痧禊＇潋狩扉铄狒趄殁螬┅趱螋磲脲轭篝犷沐趱螋戾壶卑葫栋恒镬矧㈢蝈孱轰蜥鳝骖潋狩骖┅飙篝蜷铉ㄥ疳钿疳翳⑵Ж＼⑵骗郗偏偏戚郢骗骗戚┅趱螋磲脲轭篝犷沐趱螋戾壶窗葫栋恒镬矧疱蝓轰蜥鳝骖潋狩骖┅飙篝蜷铉ㄥ疳钿疳翳⒇Ж＼⑵郢剌郗剌曝Ж＼⑵脾┅趱螋磲脲轭篝犷沐趱螋戾壶钒葫栋恒镬矧⑩蝻黝轰蜥鳝骖潋狩骖┅飙篝蜷铉ㄥ疳钿疳翳⒇Ж＼⑵圹剌剌欺曝莪丌Ж＼⑵脾┅┅ㄣ镱骈珲蝈衡徙腌蝻躅㈧轭孱ㄤ蜥鳝飙簌篝屙膊趱螋飙篝蜷铉ㄤ蜥鳝飙簌篝屙驳趱螋飙篝蜷铉博ㄤ蜥鳝飙簌篝屙驳趱螋飙篝蜷铉畅疳汶筱哄疳钿烘殪衡雉瑭筱蝻祆蝈玳镱赴栋癌┅ㄥ痫螋ы衢瞟